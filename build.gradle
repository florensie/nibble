plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url = "https://maven.terraformersmc.com" }
	maven { url "https://maven.shedaniel.me/" }
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	// Leave this as lowest priority
	maven {
		url = "https://jitpack.io"
		content {
			includeGroupByRegex "com.github.*"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	include implementation(annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}"))

	// Dev runtime
	modRuntimeOnly "curse.maven:appleskin-248787:${project.appleskin_version}"
	modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	modRuntimeOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
	modRuntimeOnly "curse.maven:lazydfu-433518:${project.lazydfu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
